# practice problems

## counting down from 4

    [ d      # save top stack value for posterity
    1-       # decrement top number
    d0<r]sr  # recurse if number is still greater than 0
    4lrx     # initialize and run macro
    f        # print resulting stack

    [d1-d0<r]sr4lrxf # single line version

## factorial of 10

    [d1-d1<r*]sr10lrxp

## fib series to one past 100

    [sbsalalblalb+d200>r]sr1 1lrxf

## fib series to first number with 1000 digits

    [sbsalalblalb+dZ1000>r]sr1 1lrxf

# euler problem 1
If we list all the natural numbers below 10 that are multiples of 3 or
5, we get 3, 5, 6 and 9. The sum of these multiples is 23.

Find the sum of all the multiples of 3 or 5 below 1000.

pretty version

    1000 1-st             # Target number: just less than 1000
    [d1+*2/]ss            # find Sum 1..n
    [sa lt la/lsx la*]sm  # find sum of Multiples of n
    3lmx 5lmx + 15lmx - f

one line version

    1000 1-st[d1+*2/]ss[saltla/lsxla*]sm3lmx5lmx+15lmx-f

# euler problem 2
Each new term in the Fibonacci sequence is generated by adding the
previous two terms. By starting with 1 and 2, the first 10 terms will
be:

1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...

By considering the terms in the Fibonacci sequence whose values do not
exceed four million, find the sum of the even-valued terms.

    [ lblalb + lrx ]sf                 # iterate fib sequence
    [ lblt+st ]ss                      # add to total
    [ sbsa lb2%0=s lalb+4000000!<f ]sr # if even execute s, if < ceiling recurse
    1 1 0st lrx ltf                    # init and run

short version

    [lblalb+lrx]sf[lblt+st]ss[sbsalb2%0=slalb+4000000!<f]sr1 1 0stlrxltf

# euler problem 3 (unsolved)
The prime factors of 13195 are 5, 7, 13 and 29.

What is the largest prime factor of the number 600851475143 ?

Answer: 6857
